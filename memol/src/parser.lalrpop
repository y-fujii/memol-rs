// (c) Yasuhiro Fujii <http://mimosa-pudica.net>, under MIT License.
use std::*;
use std::str::FromStr;
use lalrpop_util::ParseError;
use misc;
use ratio;
use ast::*;

#[LALR]
grammar<'a>( path: &path::Path );

extern {
	type Error = misc::Error;
}

pub definition: Definition<'a> = {
	<a:definition> <lo:@L> "score" <b:label> "(" ")" "=" <c:score> =>? {
		let mut a = a;
		match a.scores.insert( b, (path.to_owned(), c) ) {
			Some( _ ) => Err( ParseError::User{ error: misc::Error::new( path, lo, "duplicated definition." ) } ),
			None      => Ok( a ),
		}
	},
	<a:definition> <lo:@L> "value" <b:label> "(" ")" "=" <c:score> =>? {
		let mut a = a;
		match a.values.insert( b, (path.to_owned(), c) ) {
			Some( _ ) => Err( ParseError::User{ error: misc::Error::new( path, lo, "duplicated definition." ) } ),
			None      => Ok( a ),
		}
	},
	<a:definition> <lo:@L> "import" <b:str> =>? {
		let cpath = match path.parent() {
			Some( v ) => v.join( b ),
			None      => path::PathBuf::from( b ),
		};
		let def = parse( &cpath )
			.map_err( |e| ParseError::User{ error: e } )?;
		let mut a = a;
		a.scores.extend( def.scores );
		a.values.extend( def.values );
		Ok( a )
	},
	=> Definition{ scores: collections::HashMap::new(), values: collections::HashMap::new() },
};

score: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score> "with" <b:r"[h-z]"> "=" <c:score_stmt> <hi:@R> =>
		Ast::new_box( lo, hi, Score::With( a, b.chars().next().unwrap(), c ) ),
	<lo:@L> <a:score> "with" "*" "=" <c:score_stmt> <hi:@R> =>
		Ast::new_box( lo, hi, Score::With( a, '*', c ) ),
	<score_stmt>,
};

score_stmt: Box<Ast<Score<'a>>> = {
	<lo:@L> "if" <a:score_stmt> "then" <b:score_stmt> "else" <c:score_stmt> <hi:@R> =>
		Ast::new_box( lo, hi, Score::Branch( a, b, c ) ),
	<lo:@L> "repeat" <a:uint> <b:score_stmt> <hi:@R> =>
		Ast::new_box( lo, hi, Score::Repeat( b, a ) ),
	<lo:@L> "stretch" <a:sratio> <b:score_stmt> <hi:@R> =>
		Ast::new_box( lo, hi, Score::Stretch( b, a ) ),
	<lo:@L> "filter" <a:score_stmt> <b:score_stmt> <hi:@R> =>
		Ast::new_box( lo, hi, Score::Filter( a, b ) ),
	<score_or>,
};

score_or: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score_or> "||" <b:score_and> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Or ) ),
	<score_and>,
};

score_and: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score_and> "&&" <b:score_cmp> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Mul ) ),
	<score_cmp>,
};

score_cmp: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score_cmp> "==" <b:score_add> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Eq ) ),
	<lo:@L> <a:score_cmp> "!=" <b:score_add> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Ne ) ),
	<lo:@L> <a:score_cmp> "<=" <b:score_add> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Le ) ),
	<lo:@L> <a:score_cmp> ">=" <b:score_add> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Ge ) ),
	<lo:@L> <a:score_cmp> "<" <b:score_add> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Lt ) ),
	<lo:@L> <a:score_cmp> ">" <b:score_add> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Gt ) ),
	<score_add>,
};

score_add: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score_add> "+" <b:score_mul> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Add ) ),
	<lo:@L> <a:score_add> "-" <b:score_mul> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Sub ) ),
	<score_mul>,
};

score_mul: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score_mul> "*" <b:score_prim> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Mul ) ),
	<lo:@L> <a:score_mul> "/" <b:score_prim> <hi:@R> =>
		Ast::new_box( lo, hi, Score::BinaryOp( a, b, BinaryOp::Div ) ),
	<score_prim>,
};

score_prim: Box<Ast<Score<'a>>> = {
	<lo:@L> "_" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Score( vec![ Ast::new_box( lo, hi, Note::Rest ) ] ) ),
	<lo:@L> <a:uint> <hi:@R> => {
		let v = Some( a.into() );
		Ast::new_box( lo, hi, Score::Score( vec![ Ast::new_box( lo, hi, Note::Value( v, v ) ) ] ) )
	},
	<lo:@L> <a:label> "(" ")" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Symbol( a ) ),
	<lo:@L> "[" <a:score_list> "]" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Sequence( a ) ),
	<lo:@L> "(" <a:score_list> ")" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Parallel( a ) ),
	<lo:@L> "{" <a:score_content> "}" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Score( a ) ),
	<lo:@L> "!" <a:score_prim> <hi:@R> => {
		let v = Some( ratio::Ratio::one() );
		let one = Ast::new_box( lo, hi, Score::Score(
			vec![ Ast::new_box( lo, hi, Note::Value( v, v ) ) ]
		) );
		Ast::new_box( lo, hi, Score::BinaryOp( one, a, BinaryOp::Sub ) )
	}
};

score_list: Vec<Box<Ast<Score<'a>>>> = {
	<a:score_list> <b:score> => {
		let mut a = a;
		a.push( b );
		a
	},
	=> Vec::new(),
};

score_content: Vec<Box<Ast<Note<'a>>>> = {
	<a:score_content> "|" <lo:@L> <b:group_content> <hi:@R> => {
		let mut a = a;
		a.push( Ast::new_box( lo, hi, Note::Group( b ) ) );
		a
	},
	<lo:@L> <b:group_content> <hi:@R> => {
		let mut a = Vec::new();
		a.push( Ast::new_box( lo, hi, Note::Group( b ) ) );
		a
	},
};

note: Box<Ast<Note<'a>>> = {
	<lo:@L> "_" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Rest ),
	<lo:@L> <a:pitch_lower> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::Note( Dir::Lower, a.0, a.1, b ) ),
	<lo:@L> <a:pitch_upper> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::Note( Dir::Upper, a.0, a.1, b ) ),
	<lo:@L> <a:boundary> <hi:@R> =>
		Ast::new_box( lo, hi, Note::Value( a, a ) ),
	<lo:@L> <a:boundary> ".." <b:boundary> <hi:@R> =>
		Ast::new_box( lo, hi, Note::Value( a, b ) ),
	<lo:@L> "/" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Repeat( cell::Cell::new( None ) ) ),
	<lo:@L> "[" <a:group_content> "]" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Group( a ) ),
	<lo:@L> "(" <a:chord_content> ")" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Chord( a ) ),
	<lo:@L> <a:note> "^" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Tie( a ) ),
};

key_signature: i64 = {
	"+" <key_signature> => <> + 1,
	"-" <key_signature> => <> - 1,
	                    => 0,
};

boundary: Option<ratio::Ratio> = {
	"*"      => None,
	<a:uint> => Some( a.into() ),
};

group_content: Vec<(Box<Ast<Note<'a>>>, i64)> = {
	<a:group_content> <b:note> => {
		let mut a = a;
		a.push( (b, 1) );
		a
	},
	<a:group_content> <b:note> ":" <c:uint> => {
		let mut a = a;
		a.push( (b, c) );
		a
	},
	<a:group_content> <b:octave> => {
		let mut a = a;
		a.push( (b, 0) );
		a
	},
	=> Vec::new(),
};

chord_content: Vec<Box<Ast<Note<'a>>>> = {
	<a:chord_content> <b:note> => {
		let mut a = a;
		a.push( b );
		a
	},
	<a:chord_content> <b:octave> => {
		let mut a = a;
		a.push( b );
		a
	},
	=> Vec::new(),
};

octave: Box<Ast<Note<'a>>> = {
	<lo:@L> "<" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Octave(  1 ) ),
	<lo:@L> ">" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Octave( -1 ) ),
	<lo:@L> "@" <a:pitch_lower> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::OctaveByNote( a.0, a.1, b ) ),
	<lo:@L> "@" <a:pitch_upper> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::OctaveByNote( a.0, a.1, b ) ),
};

pitch_lower: (char, i64) = {
	<a:r"[a-g]">          => ('*', a.chars().next().unwrap() as i64 - 'a' as i64),
	<a:r"[h-z]"> <b:sint> => (a.chars().next().unwrap(), b),
};

pitch_upper: (char, i64) = {
	<a:r"[A-G]">          => ('*', a.chars().next().unwrap() as i64 - 'A' as i64),
	<a:r"[H-Z]"> <b:sint> => (a.chars().next().unwrap().to_lowercase().next().unwrap(), b),
};

sratio: ratio::Ratio = {
	<a:sint> "/" <b:uint> =>
		ratio::Ratio::new( a, b ),
	<a:sint> =>
		a.into(),
};

sint: i64 = {
	"-" <uint> => -<>,
	"+" <uint> =>  <>,
	    <uint> =>  <>,
};

uint: i64 = {
	r"[0-9]+" => i64::from_str( <> ).unwrap(),
};

label: String = {
	r"\$[a-zA-Z0-9_.]+" => String::from( &<>[1 ..] ),
};

str: String = {
	<lo:@L> <a:r#""(\\n|\\r|\\t|\\\\|[^\\"])*""#> =>? {
		let mut dst = String::new();
		let mut it = a[1 .. a.len() - 1].chars();
		while let Some( c ) = it.next() {
			dst.push( if c == '\\' {
				match it.next() {
					Some( 'n'  ) => '\n',
					Some( 'r'  ) => '\r',
					Some( 't'  ) => '\t',
					Some( '\\' ) => '\\',
					_ => panic!(),
				}
			}
			else {
				c
			} );
		}
		Ok( dst )
	},
};
