// (c) Yasuhiro Fujii <y-fujii at mimosa-pudica.net>, under MIT License.
use std::*;
use std::str::FromStr;
use lalrpop_util::ParseError;
use ratio;
use ast::*;

#[LALR]
grammar<'a>;

extern {
	type Error = (usize, &'static str);
}

pub definition: Definition<'a> = {
	<a:definition> <lo:@L> "score" <b:label> "(" ")" "=" <mi:@L> <c:score_with> <hi:@R> =>? {
		let mut a = a;
		match a.scores.insert( b, c ) {
			Some( _ ) => Err( ParseError::User{ error: (lo, "duplicated definition.") } ),
			None      => Ok( a ),
		}
	},
	<a:definition> <lo:@L> "value" <b:label> "(" ")" "=" <mi:@L> <c:value_track_eq> <hi:@R> =>? {
		let mut a = a;
		match a.values.insert( b, c ) {
			Some( _ ) => Err( ParseError::User{ error: (lo, "duplicated definition.") } ),
			None      => Ok( a ),
		}
	},
	=> Definition{ scores: collections::HashMap::new(), values: collections::HashMap::new() },
};

score_with: Box<Ast<Score<'a>>> = {
	<lo:@L> <a:score_with> "with" <b:r"[h-z]"> "=" <c:score_prim> <hi:@R> =>
		Ast::new_box( lo, hi, Score::With( a, b.chars().next().unwrap(), c ) ),
	<lo:@L> <a:score_with> "with" "_" "=" <c:score_prim> <hi:@R> =>
		Ast::new_box( lo, hi, Score::With( a, '_', c ) ),
	<score_prim>,
};

score_prim: Box<Ast<Score<'a>>> = {
	<lo:@L> "_" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Score( vec![ Ast::new_box( lo, hi, Note::Rest ) ] ) ),
	<lo:@L> <a:label> "(" ")" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Symbol( a ) ),
	<lo:@L> "[" <a:score_list> "]" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Sequence( a ) ),
	<lo:@L> "(" <a:score_list> ")" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Parallel( a ) ),
	<lo:@L> "{" <a:score_content> "}" <hi:@R> =>
		Ast::new_box( lo, hi, Score::Score( a ) ),
	<lo:@L> "repeat" <a:uint> <b:score_prim> <hi:@R> =>
		Ast::new_box( lo, hi, Score::Sequence( (0 .. a).map( |_| b.clone() ).collect() ) ),
	<lo:@L> <a:uratio> ":" <b:score_prim> <hi:@R> =>
		Ast::new_box( lo, hi, Score::Stretch( b, a ) ),
};

score_list: Vec<Box<Ast<Score<'a>>>> = {
	<a:score_list> <b:score_with> => {
		let mut a = a;
		a.push( b );
		a
	},
	=> Vec::new(),
};

score_content: Vec<Box<Ast<Note<'a>>>> = {
	<a:score_content> "|" <lo:@L> <b:group_content> <hi:@R> => {
		let mut a = a;
		a.push( Ast::new_box( lo, hi, Note::Group( b ) ) );
		a
	},
	<lo:@L> <b:group_content> <hi:@R> => {
		let mut a = Vec::new();
		a.push( Ast::new_box( lo, hi, Note::Group( b ) ) );
		a
	},
};

note: Box<Ast<Note<'a>>> = {
	<lo:@L> "_" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Rest ),
	<lo:@L> "/" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Repeat( cell::Cell::new( None ) ) ),
	<lo:@L> <a:pitch_lower> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::Note( Dir::Lower, a.0, a.1, b ) ),
	<lo:@L> <a:pitch_upper> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::Note( Dir::Upper, a.0, a.1, b ) ),
	<lo:@L> "[" <a:group_content> "]" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Group( a ) ),
	<lo:@L> "(" <a:chord_content> ")" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Chord( a ) ),
	<lo:@L> <a:note> "^" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Tie( a ) ),
};

octave: Box<Ast<Note<'a>>> = {
	<lo:@L> "<" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Octave(  1 ) ),
	<lo:@L> ">" <hi:@R> =>
		Ast::new_box( lo, hi, Note::Octave( -1 ) ),
	<lo:@L> "@" <a:pitch_lower> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::OctaveByNote( a.0, a.1, b ) ),
	<lo:@L> "@" <a:pitch_upper> <b:key_signature> <hi:@R> =>
		Ast::new_box( lo, hi, Note::OctaveByNote( a.0, a.1, b ) ),
};

key_signature: i32 = {
	"+" <key_signature> => <> + 1,
	"-" <key_signature> => <> - 1,
	                    => 0,
};

group_content: Vec<(Box<Ast<Note<'a>>>, i32)> = {
	<a:group_content> <b:note> => {
		let mut a = a;
		a.push( (b, 1) );
		a
	},
	<a:group_content> <c:uint> ":" <b:note> => {
		let mut a = a;
		a.push( (b, c) );
		a
	},
	<a:group_content> <c:uint> <b:note> => {
		let mut a = a;
		a.push( (b, c) );
		a
	},
	<a:group_content> <b:octave> => {
		let mut a = a;
		a.push( (b, 0) );
		a
	},
	=> Vec::new(),
};

chord_content: Vec<Box<Ast<Note<'a>>>> = {
	<a:chord_content> <b:note> => {
		let mut a = a;
		a.push( b );
		a
	},
	<a:chord_content> <b:octave> => {
		let mut a = a;
		a.push( b );
		a
	},
	=> Vec::new(),
};

value_track_eq: Box<Ast<ValueTrack>> = {
	<lo:@L> <a:value_track_eq> "==" <b:value_track_add> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Eq ) ),
	<lo:@L> <a:value_track_eq> "!=" <b:value_track_add> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Ne ) ),
	<lo:@L> <a:value_track_eq> "<=" <b:value_track_add> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Le ) ),
	<lo:@L> <a:value_track_eq> ">=" <b:value_track_add> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Ge ) ),
	<lo:@L> <a:value_track_eq> "<" <b:value_track_add> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Lt ) ),
	<lo:@L> <a:value_track_eq> ">" <b:value_track_add> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Gt ) ),
	<value_track_add>,
};

value_track_add: Box<Ast<ValueTrack>> = {
	<lo:@L> <a:value_track_add> "+" <b:value_track_mul> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Add ) ),
	<lo:@L> <a:value_track_add> "-" <b:value_track_mul> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Sub ) ),
	<value_track_mul>,
};

value_track_mul: Box<Ast<ValueTrack>> = {
	<lo:@L> <a:value_track_mul> "*" <b:value_track_prim> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Mul ) ),
	<lo:@L> <a:value_track_mul> "/" <b:value_track_prim> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::BinaryOp( a, b, BinaryOp::Div ) ),
	<value_track_prim>,
};

value_track_prim: Box<Ast<ValueTrack>> = {
	<lo:@L> <a:label> "(" ")" <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::Symbol( a ) ),
	<lo:@L> "[" <a:value_track_list> "]" <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::Sequence( a ) ),
	<lo:@L> "{" <a:value_track_content> "}" <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::ValueTrack( a ) ),
	<lo:@L> <a:uratio> ":" <b:value_track_prim> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::Stretch( b, a ) ),
	<lo:@L> "repeat" <a:uint> <b:value_track_prim> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::Sequence( (0 .. a).map( |_| b.clone() ).collect() ) ),
	<lo:@L> "if" <a:value_track_eq> "then" <b:value_track_prim> "else" <c:value_track_prim> <hi:@R> =>
		Ast::new_box( lo, hi, ValueTrack::Branch( a, b, c ) ),
};

value_track_list: Vec<Box<Ast<ValueTrack>>> = {
	<a:value_track_list> <b:value_track_eq> => {
		let mut a = a;
		a.push( b );
		a
	},
	=> Vec::new(),
};

value_track_content: Vec<Box<Ast<Value>>> = {
	<a:value_track_content> "|" <lo:@L> <b:value_group_content> <hi:@R> => {
		let mut a = a;
		a.push( Ast::new_box( lo, hi, Value::Group( b ) ) );
		a
	},
	<lo:@L> <b:value_group_content> <hi:@R> => {
		let mut a = Vec::new();
		a.push( Ast::new_box( lo, hi, Value::Group( b ) ) );
		a
	},
};

value: Box<Ast<Value>> = {
	<lo:@L> <a:sratio> <hi:@R> =>
		Ast::new_box( lo, hi, Value::Value( a, a ) ),
	<lo:@L> <a:sratio> ".." <b:sratio> <hi:@R> =>
		Ast::new_box( lo, hi, Value::Value( a, b ) ),
	<lo:@L> "[" <a:value_group_content> "]" <hi:@R> =>
		Ast::new_box( lo, hi, Value::Group( a ) ),
};

value_group_content: Vec<(Box<Ast<Value>>, i32)> = {
	<a:value_group_content> <b:value> => {
		let mut a = a;
		a.push( (b, 1) );
		a
	},
	<a:value_group_content> <c:uint> ":" <b:value> => {
		let mut a = a;
		a.push( (b, c) );
		a
	},
	=> Vec::new(),
};

pitch_lower: (char, i32) = {
	<a:r"[a-g]">          => ('_', a.chars().next().unwrap() as i32 - 'a' as i32),
	<a:r"[h-z]"> <b:sint> => (a.chars().next().unwrap(), b),
};

pitch_upper: (char, i32) = {
	<a:r"[A-G]">          => ('_', a.chars().next().unwrap() as i32 - 'A' as i32),
	<a:r"[H-Z]"> <b:sint> => (a.chars().next().unwrap().to_lowercase().next().unwrap(), b),
};

sratio: ratio::Ratio = {
	<a:sint> "/" <b:uint> =>
		ratio::Ratio::new( a as i64, b as i64 ),
	<a:sint> =>
		ratio::Ratio::new( a as i64, 1 ),
};

uratio: ratio::Ratio = {
	<a:uint> "/" <b:uint> =>
		ratio::Ratio::new( a as i64, b as i64 ),
	<a:uint> =>
		ratio::Ratio::new( a as i64, 1 ),
};

sint: i32 = {
	"-" <uint> => -<>,
	"+" <uint> =>  <>,
	    <uint> =>  <>,
};

uint: i32 = {
	r"[0-9]+" => i32::from_str( <> ).unwrap(),
};

label: String = {
	r"\$[a-zA-Z0-9_.]+" => String::from( &<>[1..] ),
};
